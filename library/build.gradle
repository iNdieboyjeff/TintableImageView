buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.13'
    }

}

plugins {
    id "com.jfrog.bintray" version "1.4"
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

allprojects {
    group = 'util.android.tintableimageview'
    version = '27.1.1'
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url  "http://dl.bintray.com/indieboyjeff/maven"
    }

}

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.0 rc2'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:27.1.1'
}

def siteUrl = 'https://github.com/iNdieboyjeff/TintableImageView'      // Homepage URL of the library
def gitUrl = 'https://github.com/iNdieboyjeff/TintableImageView.git'   // Git repository URL
def issueUrl = 'https://github.com/iNdieboyjeff/TintableImageView/issues'


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    title = 'TintableImageView ' + version
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    exclude '**/BuildConfig.java'
    options {
        links "http://docs.oracle.com/javase/7/docs/api/"
        linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference"
    }
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
afterEvaluate {
    javadoc.classpath += project.android.libraryVariants.toList().first().javaCompile.classpath
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "tintableimageview"
        websiteUrl = siteUrl
        publicDownloadNumbers = true
        vcsUrl = gitUrl
        issueTrackerUrl = issueUrl
        labels = ["Android", "ImageView", "Tint", "Widget"]
        desc = "Custom ImageView component for Android."
        licenses = ["Apache-2.0"]
        publish = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:27.1.1'
}
